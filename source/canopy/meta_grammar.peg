grammar Canopy.MetaGrammar

grammar                   <-  __* grammar_name
                              rules:(__* grammar_rule)+ __*
                              <Grammar>

grammar_name              <-  `grammar` ":"? __+ object_identifier

grammar_rule              <-  identifier assignment parsing_expression <GrammarRule>

assignment                <-  __+ "<-" __+

parsing_expression        <-  choice_expression / choice_part

parenthesised_expression  <-  "(" __* parsing_expression __* ")"

choice_expression         <-  first_part:choice_part
                              rest:(__+ "/" __+ expression:choice_part)+
                              <Choice>

choice_part               <-  (action_expression / sequence_expression / sequence_part)
                              (__+ type_tag)?
                              <ChoicePart>

action_expression         <-  actionable_expression __+ action_tag <Action>

actionable_expression     <-  "(" __* actionable_expression __* ")"
                           /  sequence_expression
                           /  repeated_atom
                           /  terminal_node

action_tag                <-  "%" identifier

type_tag                  <-  "<" object_identifier ">"

sequence_expression       <-  first_part:sequence_part
                              rest:(__+ expression:sequence_part)+
                              <Sequence>

sequence_part             <-  label? expression:(maybe_atom / repeated_atom / atom) <SequencePart>

maybe_atom                <-  atom "?" <Maybe>

repeated_atom             <-  atom quantifier:("*" / "+") <Repeat>

atom                      <-  parenthesised_expression
                           /  predicated_atom
                           /  reference_expression
                           /  terminal_node

terminal_node             <-  string_expression
                           /  ci_string_expression
                           /  char_class_expression
                           /  any_char_expression

predicated_atom           <-  predicate:("&" / "!") atom <Predicate>

reference_expression      <-  identifier !assignment <Reference>

string_expression         <-  ('"' ("\\" . / [^"])* '"' / "'" ("\\" . / [^'])* "'") <String>

ci_string_expression      <-  "`" ("\\" . / [^`])* "`" <CIString>

any_char_expression       <-  "." <AnyChar>

char_class_expression     <-  "[" "^"? ("\\" . / [^\]])+ "]" <CharClass>

label                     <-  identifier ":"

object_identifier         <-  identifier ("." identifier)*

identifier                <-  [a-zA-Z_] [a-zA-Z0-9_]*

__                        <-  [\s] / comment

comment                   <-  "#" [^\n]*
